#include <Servo.h>

#define trigPin 6
#define echoPin 7

#define IN1 8
#define IN2 9
#define IN3 10
#define IN4 11

Servo myServo;

long durasi;
int jarak;
int jarakKanan;
int jarakKiri;

// Fungsi untuk mengukur jarak
int ukurJarak() {
  digitalWrite(trigPin, LOW);
  delayMicroseconds(2);
  digitalWrite(trigPin, HIGH);
  delayMicroseconds(10);
  digitalWrite(trigPin, LOW);

  durasi = pulseIn(echoPin, HIGH);
  return durasi * 0.034 / 2; // jarak dalam cm
}

// === Fungsi Gerak Motor ===
void maju() {
  digitalWrite(IN1, HIGH);
  digitalWrite(IN2, LOW);
  digitalWrite(IN3, HIGH);
  digitalWrite(IN4, LOW);
}

void mundur() {
  digitalWrite(IN1, LOW);
  digitalWrite(IN2, HIGH);
  digitalWrite(IN3, LOW);
  digitalWrite(IN4, HIGH);
}

void kiri() {
  digitalWrite(IN1, LOW);
  digitalWrite(IN2, HIGH);
  digitalWrite(IN3, HIGH);
  digitalWrite(IN4, LOW);
}

void kanan() {
  digitalWrite(IN1, HIGH);
  digitalWrite(IN2, LOW);
  digitalWrite(IN3, LOW);
  digitalWrite(IN4, HIGH);
}

void berhenti() {
  digitalWrite(IN1, LOW);
  digitalWrite(IN2, LOW);
  digitalWrite(IN3, LOW);
  digitalWrite(IN4, LOW);
}

void setup() {
  pinMode(trigPin, OUTPUT);
  pinMode(echoPin, INPUT);

  pinMode(IN1, OUTPUT);
  pinMode(IN2, OUTPUT);
  pinMode(IN3, OUTPUT);
  pinMode(IN4, OUTPUT);

  myServo.attach(5); // Pin untuk kabel kuning pada servo
  myServo.write(90); // posisi tengah
  delay(1000);

  Serial.begin(9600);
}

void loop() {
  jarak = ukurJarak();
  Serial.print("Jarak Depan: ");
  Serial.println(jarak);

  if (jarak > 20) { // Jika jarak lebih jauh dari 20 cm maka mobil akan terus maju
    maju();
  } else {          // Jika jarak kurang dari 20 cm mobil akan...
    berhenti();     // Berhenti
    delay(500);

    // Ukur arah kanan
    myServo.write(0);
    delay(2000);
    jarakKanan = ukurJarak();
    Serial.print("Kanan: ");
    Serial.println(jarakKanan);

    // Ukur arah kiri
    myServo.write(180);
    delay(2000);
    jarakKiri = ukurJarak();
    Serial.print("Kiri: ");
    Serial.println(jarakKiri);

    // kembali mengarah ke depan
    myServo.write(90);
    delay(500);

    // Menentukan arah belok
    if (jarakKiri > jarakKanan) {
      Serial.println("belok kiri");
      kiri();
      delay(600);
    } else {
      Serial.println("belok kanan");
      kanan();
      delay(600);
    }

    berhenti();
    delay(300);
  }
}
